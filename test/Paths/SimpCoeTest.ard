\import Meta
\import Paths
\import Paths.Meta

\func funTransportTest {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (f : B a -> C a) (g : B a' -> C a') (t : \Pi (b : B a) -> transport C p (f b) = g (transport B p b)) : transport (\lam x => B x -> C x) p f = g
  => simp_coe t

\func funCoeTest {A1 A2 : \Type} (B C : A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : B a1 a2 -> C a1 a2) (g : B a1' a2' -> C a1' a2')
                 (t : \Pi (b : B a1 a2) -> coe (\lam i => C (p1 @ i) (p2 @ i)) (f b) right = g (coe (\lam i => B (p1 @ i) (p2 @ i)) b right)) : coe (\lam i => B (p1 @ i) (p2 @ i) -> C (p1 @ i) (p2 @ i)) f right = g
  => simp_coe t

\func funTransportTest2 {A : \Type} (B1 B2 B3 B4 : A -> \Type) {a a' : A} (p : a = a') (f : (B1 a -> B2 a) -> B3 a -> B4 a) (g : (B1 a' -> B2 a') -> B3 a' -> B4 a')
                        (t : \Pi (h : B1 a' -> B2 a) (y : B3 a) -> transport B4 p (f (\lam x => h (transport B1 p x)) y) = g (\lam x => transport B2 p (h x)) (transport B3 p y)) : transport (\lam x => (B1 x -> B2 x) -> B3 x -> B4 x) p f = g
  => simp_coe t

\func piTransportTest {A B : \Type} (C : B -> A -> \Type) {a a' : A} (p : a = a') (f : \Pi (y : B) -> C y a) (g : \Pi (y : B) -> C y a') (t : \Pi (b : B) -> transport (C b) p (f b) = g b) : transport (\lam x => \Pi (y : B) -> C y x) p f = g
  => simp_coe t

\func piCoeTest {A1 A2 B : \Type} (C : B -> A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : \Pi (y : B) -> C y a1 a2) (g : \Pi (y : B) -> C y a1' a2')
                (t : \Pi (b : B) -> coe (\lam i => C b (p1 @ i) (p2 @ i)) (f b) right = g b) : coe (\lam i => \Pi (y : B) -> C y (p1 @ i) (p2 @ i)) f right = g
  => simp_coe t

\func piTransportTest2 {A B : \Type} (C : B -> A -> \Type) (D : A -> \Type) {a a' : A} (p : a = a') (f : D a -> \Pi (y : B) -> C y a) (g : D a' -> \Pi (y : B) -> C y a')
                       (t : \Pi (d : D a) (b : B) -> transport (C b) p (f d b) = g (transport D p d) b) : transport (\lam x => D x -> \Pi (y : B) -> C y x) p f = g
  => simp_coe t

\func piTransportTest3 {A B : \Type} (C : B -> A -> \Type) (D : B -> A -> \Type) {a a' : A} (p : a = a') (f : \Pi (y : B) -> D y a -> C y a) (g : \Pi (y : B) -> D y a' -> C y a')
                       (t : \Pi (b : B) (d : D b a) -> transport (C b) p (f b d) = g b (transport (D b) p d)) : transport (\lam x => \Pi (y : B) -> D y x -> C y x) p f = g
  => simp_coe t

\func piTransportArgTest {A B : \Type} (C : B -> A -> \Type) {a a' : A} (p : a = a') (f : \Pi (y : B) -> C y a) (b : B) (c : C b a') (t : transport (C b) p (f b) = c) : transport (\lam x => \Pi (y : B) -> C y x) p f b = c
  => simp_coe t

\func piCoeArgTest {A1 A2 B : \Type} (C : B -> A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : \Pi (y : B) -> C y a1 a2) (b : B) (c : C b a1' a2')
                   (t : coe (\lam i => C b (p1 @ i) (p2 @ i)) (f b) right = c) : coe (\lam i => \Pi (y : B) -> C y (p1 @ i) (p2 @ i)) f right b = c
  => simp_coe t

\func projTransportTest {A B : \Type} (C : B -> A -> \Type) {a a' : A} (p : a = a') (t : \Sigma (b : B) (C b a)) (b : B) (h : t.1 = b) : (transport (\lam x => \Sigma (b : B) (C b x)) p t).1 = b
  => simp_coe h

\func projCoeTest {B : \Type} (C : B -> I -> \Type) (t : \Sigma (b : B) (C b left)) (b : B) (h : t.1 = b) : (coe (\lam i => \Sigma (b : B) (C b i)) t right).1 = b
  => simp_coe h

\func projTransportTest2 {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (t : \Sigma (B a) (C a)) (c : C a') (h : transport C p t.2 = c) : (transport (\lam x => \Sigma (B x) (C x)) p t).2 = c
  => simp_coe h

\func projCoeTest2 (B C : I -> \Type) (t : \Sigma (B left) (C left)) (c : C right) (h : coe C t.2 right = c) : (coe (\lam i => \Sigma (B i) (C i)) t right).2 = c
  => simp_coe h

\func tupleTransportTest {A : \Type} (B C : A -> \Type) (D : \Pi (x : A) -> B x -> C x -> \Prop) {a a' : A} (p : a = a') (t : \Sigma (b : B a) (c : C a) (D a b c)) (s : \Sigma (b : B a') (c : C a') (D a' b c))
                         (h1 : transport B p t.1 = s.1) (h2 : transport C p t.2 = s.2) : transport (\lam x => \Sigma (b : B x) (c : C x) (D x b c)) p t = s
  => simp_coe (h1,h2)

\func tupleCoeTest {A : \Type} (B C : I -> \Type) (D : \Pi (i : I) -> B i -> C i -> \Prop) (t : \Sigma (b : B left) (c : C left) (D left b c)) (s : \Sigma (b : B right) (c : C right) (D right b c))
                   (h1 : coe B t.1 right = s.1) (h2 : coe C t.2 right = s.2) : coe (\lam i => \Sigma (b : B i) (c : C i) (D i b c)) t right = s
  => simp_coe (h1,h2)

\func tupleTransportTest2 {A : \Type} (B : A -> \Type) (C : \Pi (x : A) -> B x -> \Prop) {a a' : A} (p : a = a') (t : \Sigma (b : B a) (c : C a b)) (s : \Sigma (b : B a') (c : C a' b))
                          (h : transport B p t.1 = s.1) : transport (\lam x => \Sigma (b : B x) (c : C x b)) p t = s
  => simp_coe h

\func fieldTransportTest {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (r : R B C a) (c : C a') (h : transport C p r.c = c) : R.c {transport (R B C) p r} = c
  => simp_coe h
  \where {
    \record R {A : \Type} (B C : A -> \Type) (a : A) (b : B a) (c : C a)
  }

\func fieldCoeTest (B C : I -> \Type) (r : R B C left) (c : C right) (h : coe C r.c right = c) : R.c {coe (R B C) r right} = c
  => simp_coe h
  \where {
    \record R (B C : I -> \Type) (j : I) (b : B j) (c : C j)
  }

\func recordTransportTest {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (r : R B C a) (r' : R B C a') (h1 : transport B p r.b = r'.b) (h2 : transport C p r.c = r'.c) : transport (R B C) p r = r'
  => simp_coe (h1,h2)
  \where {
    \record R {A : \Type} (B C : A -> \Type) (a : A) (b : B a) (c : C a)
  }

\func recordCoeTest (B C : I -> \Type) (r : R B C left) (r' : R B C right) (h1 : coe B r.b right = r'.b) (h2 : coe C r.c right = r'.c) : coe (R B C) r right = r'
  => simp_coe (h1,h2)
  \where {
    \record R (B C : I -> \Type) (j : I) (b : B j) (c : C j)
  }

\func recordTransportTest2 {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (r : R B C a) (r' : R B C a') (h1 : transport B p r.b = r'.b) (h2 : transport C p r.c = r'.c) : transport (R B C) p r = r'
  => simp_coe R { | b => h1 | c => h2 }
  \where {
    \record R {A : \Type} (B C : A -> \Type) (a : A) (b : B a) (c : C a)
  }

\func pathTransportTest {A B : \Type} (f g : A -> B) {a a' : A} (p : a = a') (q : f a = g a) (s : f a' = g a') (h : q *> pmap g p = pmap f p *> s) : transport (\lam x => f x = g x) p q = s
  => simp_coe h

\func pathTransportTest2 {A B : \Type} (b : B) (g : A -> B) {a a' : A} (p : a = a') (q : b = g a) (s : b = g a') (h : q *> pmap g p = s) : transport (\lam x => b = g x) p q = s
  => simp_coe h

\func pathCoeTest {B : \Type} (f g : I -> B) (q : f left = g left) (s : f right = g right) (h : q *> path g = path f *> s) : coe (\lam i => f i = g i) q right = s
  => simp_coe h

\func pathCoeTest2 {B : \Type} (b : B) (g : I -> B) (q : b = g left) (s : b = g right) (h : q *> path g = s) : coe (\lam i => b = g i) q right = s
  => simp_coe h

\func argFunTransportTest {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (f : B a -> C a) (g : B a' -> C a') (t : transport (\lam x => B x -> C x) p f = g) : \Pi (b : B a) -> transport C p (f b) = g (transport B p b)
  => simp_coe in t

\func argFunTransportTest' {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (f : B a -> C a) (g : B a' -> C a') (t : transport (\lam x => B x -> C x) p f = g) (b : B a) : transport C p (f b) = g (transport B p b)
  => (simp_coe in t) b

\func argFunCoeTest {A1 A2 : \Type} (B C : A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : B a1 a2 -> C a1 a2) (g : B a1' a2' -> C a1' a2')
                    (t : coe (\lam i => B (p1 @ i) (p2 @ i) -> C (p1 @ i) (p2 @ i)) f right = g) : \Pi (b : B a1 a2) -> coe (\lam i => C (p1 @ i) (p2 @ i)) (f b) right = g (coe (\lam i => B (p1 @ i) (p2 @ i)) b right)
  => simp_coe in t

\func argFunTransportTest2 {A : \Type} (B1 B2 B3 B4 : A -> \Type) {a a' : A} (p : a = a') (f : (B1 a -> B2 a) -> B3 a -> B4 a) (g : (B1 a' -> B2 a') -> B3 a' -> B4 a')
                           (t : transport (\lam x => (B1 x -> B2 x) -> B3 x -> B4 x) p f = g) : \Pi (h : B1 a' -> B2 a) (y : B3 a) -> transport B4 p (f (\lam x => h (transport B1 p x)) y) = g (\lam x => transport B2 p (h x)) (transport B3 p y)
  => simp_coe in t

\func argFunTransportTest2' {A : \Type} (B1 B2 B3 B4 : A -> \Type) {a a' : A} (p : a = a') (f : (B1 a -> B2 a) -> B3 a -> B4 a) (g : (B1 a' -> B2 a') -> B3 a' -> B4 a')
                            (t : transport (\lam x => (B1 x -> B2 x) -> B3 x -> B4 x) p f = g) (h : B1 a' -> B2 a) (y : B3 a) : transport B4 p (f (\lam x => h (transport B1 p x)) y) = g (\lam x => transport B2 p (h x)) (transport B3 p y)
  => (simp_coe in t) h y

\func argFunTransportTest2'' {A : \Type} (B1 B2 B3 B4 : A -> \Type) {a a' : A} (p : a = a') (f : (B1 a -> B2 a) -> B3 a -> B4 a) (g : (B1 a' -> B2 a') -> B3 a' -> B4 a')
                             (t : transport (\lam x => (B1 x -> B2 x) -> B3 x -> B4 x) p f = g) (h : B1 a' -> B2 a) : \Pi (y : B3 a) -> transport B4 p (f (\lam x => h (transport B1 p x)) y) = g (\lam x => transport B2 p (h x)) (transport B3 p y)
  => (simp_coe in t) h

\func argPiTransportTest {A B : \Type} (C : B -> A -> \Type) {a a' : A} (p : a = a') (f : \Pi (y : B) -> C y a) (g : \Pi (y : B) -> C y a') (t : transport (\lam x => \Pi (y : B) -> C y x) p f = g) : \Pi (b : B) -> transport (C b) p (f b) = g b
  => simp_coe in t

\func argPiCoeTest {A1 A2 B : \Type} (C : B -> A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : \Pi (y : B) -> C y a1 a2) (g : \Pi (y : B) -> C y a1' a2')
                   (t : coe (\lam i => \Pi (y : B) -> C y (p1 @ i) (p2 @ i)) f right = g) : \Pi (b : B) -> coe (\lam i => C b (p1 @ i) (p2 @ i)) (f b) right = g b
  => simp_coe in t

\func argPiCoeTest' {A1 A2 B : \Type} (C : B -> A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : \Pi (y : B) -> C y a1 a2) (g : \Pi (y : B) -> C y a1' a2')
                    (t : coe (\lam i => \Pi (y : B) -> C y (p1 @ i) (p2 @ i)) f right = g) (b : B) : coe (\lam i => C b (p1 @ i) (p2 @ i)) (f b) right = g b
  => (simp_coe in t) b

\func argPiTransportTest2 {A B : \Type} (C : B -> A -> \Type) (D : A -> \Type) {a a' : A} (p : a = a') (f : D a -> \Pi (y : B) -> C y a) (g : D a' -> \Pi (y : B) -> C y a')
                          (t : transport (\lam x => D x -> \Pi (y : B) -> C y x) p f = g) : \Pi (d : D a) (b : B) -> transport (C b) p (f d b) = g (transport D p d) b
  => simp_coe in t

\func argPiTransportTest3 {A B : \Type} (C : B -> A -> \Type) (D : B -> A -> \Type) {a a' : A} (p : a = a') (f : \Pi (y : B) -> D y a -> C y a) (g : \Pi (y : B) -> D y a' -> C y a')
                          (t : transport (\lam x => \Pi (y : B) -> D y x -> C y x) p f = g) : \Pi (b : B) (d : D b a) -> transport (C b) p (f b d) = g b (transport (D b) p d)
  => simp_coe in t

\func argPiTransportArgTest {A B : \Type} (C : B -> A -> \Type) {a a' : A} (p : a = a') (f : \Pi (y : B) -> C y a) (b : B) (c : C b a') (t : transport (\lam x => \Pi (y : B) -> C y x) p f b = c) : transport (C b) p (f b) = c
  => simp_coe in t

\func argPiCoeArgTest {A1 A2 B : \Type} (C : B -> A1 -> A2 -> \Type) {a1 a1' : A1} {a2 a2' : A2} (p1 : a1 = a1') (p2 : a2 = a2') (f : \Pi (y : B) -> C y a1 a2) (b : B) (c : C b a1' a2')
                      (t : coe (\lam i => \Pi (y : B) -> C y (p1 @ i) (p2 @ i)) f right b = c) : coe (\lam i => C b (p1 @ i) (p2 @ i)) (f b) right = c
  => simp_coe in t

\func argProjTransportTest {A B : \Type} (C : B -> A -> \Type) {a a' : A} (p : a = a') (t : \Sigma (b : B) (C b a)) (b : B) (h : (transport (\lam x => \Sigma (b : B) (C b x)) p t).1 = b)
  : t.1 = b
  => simp_coe in h

\func argProjCoeTest {B : \Type} (C : B -> I -> \Type) (t : \Sigma (b : B) (C b left)) (b : B) (h : (coe (\lam i => \Sigma (b : B) (C b i)) t right).1 = b) : t.1 = b
  => simp_coe in h

\func argProjTransportTest2 {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (t : \Sigma (B a) (C a)) (c : C a') (h : (transport (\lam x => \Sigma (B x) (C x)) p t).2 = c) : transport C p t.2 = c
  => simp_coe in h

\func argProjCoeTest2 (B C : I -> \Type) (t : \Sigma (B left) (C left)) (c : C right) (h : (coe (\lam i => \Sigma (B i) (C i)) t right).2 = c) : coe C t.2 right = c
  => simp_coe in h

\func argTupleTransportTest {A : \Type} (B C : A -> \Type) (D : \Pi (x : A) -> B x -> C x -> \Prop) {a a' : A} (p : a = a') (t : \Sigma (b : B a) (c : C a) (D a b c)) (s : \Sigma (b : B a') (c : C a') (D a' b c))
                            (h : transport (\lam x => \Sigma (b : B x) (c : C x) (D x b c)) p t = s) : \Sigma (transport B p t.1 = s.1) (transport C p t.2 = s.2)
  => simp_coe in h

\func argTupleCoeTest {A : \Type} (B C : I -> \Type) (D : \Pi (i : I) -> B i -> C i -> \Prop) (t : \Sigma (b : B left) (c : C left) (D left b c)) (s : \Sigma (b : B right) (c : C right) (D right b c))
                      (h : coe (\lam i => \Sigma (b : B i) (c : C i) (D i b c)) t right = s) : \Sigma (coe B t.1 right = s.1) (coe C t.2 right = s.2)
  => simp_coe in h

\func argTupleTransportTest2 {A : \Type} (B : A -> \Type) (C : \Pi (x : A) -> B x -> \Prop) {a a' : A} (p : a = a') (t : \Sigma (b : B a) (c : C a b)) (s : \Sigma (b : B a') (c : C a' b))
                             (h : transport (\lam x => \Sigma (b : B x) (c : C x b)) p t = s) : transport B p t.1 = s.1
  => simp_coe in h

\func argFieldTransportTest {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (r : R B C a) (c : C a') (h : R.c {transport (R B C) p r} = c) : transport C p r.c = c
  => simp_coe in h
  \where \open fieldTransportTest

\func argFieldCoeTest (B C : I -> \Type) (r : R B C left) (c : C right) (h : R.c {coe (R B C) r right} = c) : coe C r.c right = c
  => simp_coe in h
  \where \open fieldCoeTest

\func argRecordTransportTest {A : \Type} (B C : A -> \Type) {a a' : A} (p : a = a') (r : R B C a) (r' : R B C a') (h : transport (R B C) p r = r') : \Sigma (transport B p r.b = r'.b) (transport C p r.c = r'.c)
  => simp_coe in h
  \where \open recordTransportTest

\func argRecordCoeTest (B C : I -> \Type) (r : R B C left) (r' : R B C right) (h : coe (R B C) r right = r') : \Sigma (coe B r.b right = r'.b) (coe C r.c right = r'.c)
  => simp_coe in h
  \where {
    \record R (B C : I -> \Type) (j : I) (b : B j) (c : C j)
  }

\func argPathTransportTest {A B : \Type} (f g : A -> B) {a a' : A} (p : a = a') (q : f a = g a) (s : f a' = g a') (h : transport (\lam x => f x = g x) p q = s)
  : q *> pmap g p = pmap f p *> s
  => simp_coe in h

\func argPathTransportTest' {A B : \Type} (f g : A -> B) {a a' : A} (p : a = a') (q : f a = g a) (s : f a' = g a') (h : transport (\lam x => f x = g x) p q = s)
  : q *> pmap g p = pmap f p *> s
  => \let t => simp_coe h \in t

\func argPathTransportTest2 {A B : \Type} (b : B) (g : A -> B) {a a' : A} (p : a = a') (q : b = g a) (s : b = g a') (h : transport (\lam x => b = g x) p q = s) : q *> pmap g p = s
  => simp_coe in h

\func argPathCoeTest {B : \Type} (f g : I -> B) (q : f left = g left) (s : f right = g right) (h : coe (\lam i => f i = g i) q right = s) : q *> path g = path f *> s
  => simp_coe in h

\func argPathCoeTest2 {B : \Type} (b : B) (g : I -> B) (q : b = g left) (s : b = g right) (h : coe (\lam i => b = g i) q right = s) : q *> path g = s
  => simp_coe in h
